apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

group = 'com.github.lykmapipo'

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 120
        versionName "0.12.0"
        consumerProguardFiles 'proguard-rules.pro'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }
}


dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    api "com.android.support:support-v4:${APPCOMPAT_VERSION}"
    api "com.android.support:appcompat-v7:${APPCOMPAT_VERSION}"
    implementation "com.google.android.gms:play-services-base:${PLAY_SERVICE_BASE_VERSION}"
    api "com.google.firebase:firebase-core:${FIREBASE_CORE_VERSION}"
    api "com.google.firebase:firebase-messaging:${FIREBASE_MESSAGING_VERSION}"
    implementation "com.squareup.retrofit2:retrofit:${RETROFIT_VERSION}"
    implementation "com.squareup.retrofit2:converter-gson:${RETROFIT_VERSION}"
    api "com.github.lykmapipo:local-burst:${LOCALBURST_VERSION}"
    api "com.github.lykmapipo:android-preference:${ANDROID_PREFERENCE_VERSION}"

    //test dependencies
    testImplementation "junit:junit:${JUNIT_VERSION}"
    testImplementation "org.mockito:mockito-core:${MOCKITO_VERSION}"
    testImplementation "com.squareup.retrofit2:retrofit-mock:${RETROFIT_VERSION}"
    testImplementation "org.robolectric:robolectric:${ROBOLECTRIC_VERSION}"
}

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

// uncomment to build a jar file in addition to the default aar file
//android.libraryVariants.all { variant ->
//    def name = variant.buildType.name
//    def task = project.tasks.create "jar${name.capitalize()}", Jar
//    task.dependsOn variant.javaCompile
//    task.from variant.javaCompile.destinationDir
//    artifacts.add('archives', task);
//}

