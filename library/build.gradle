apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

group = 'com.github.lykmapipo'

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 130
        versionName "0.13.0"
        consumerProguardFiles 'proguard-rules.pro'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}


dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    api "androidx.annotation:annotation:${ANDROIDX_ANNOTATION_VERSION}"
    api "androidx.legacy:legacy-support-v4:${ANDROIDX_LEGACY_SUPPORT_VERSION}"
    api "androidx.appcompat:appcompat:${ANDROIDX_APPCOMPAT_VERSION}"
    api "com.google.android.gms:play-services-base:${PLAY_SERVICE_BASE_VERSION}"
    api "com.google.firebase:firebase-core:${FIREBASE_CORE_VERSION}"
    api "com.google.firebase:firebase-messaging:${FIREBASE_MESSAGING_VERSION}"
    api "com.squareup.retrofit2:retrofit:${RETROFIT_VERSION}"
    api "com.squareup.retrofit2:converter-gson:${RETROFIT_VERSION}"
    api "com.github.lykmapipo:local-burst:${LOCALBURST_VERSION}"
    api "com.github.lykmapipo:android-preference:${ANDROID_PREFERENCE_VERSION}"
    api "com.github.lykmapipo:retrofit-common:${RETROFIT_COMMON_VERSION}"

    // test dependencies
    testImplementation "junit:junit:${JUNIT_VERSION}"
    testImplementation "org.mockito:mockito-core:${MOCKITO_VERSION}"
    testImplementation "org.robolectric:robolectric:${ROBOLECTRIC_VERSION}"
    testImplementation "androidx.test:core:${ANDROIDX_TEST_CORE}"
}

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

// uncomment to build a jar file in addition to the default aar file
//android.libraryVariants.all { variant ->
//    def name = variant.buildType.name
//    def task = project.tasks.create "jar${name.capitalize()}", Jar
//    task.dependsOn variant.javaCompile
//    task.from variant.javaCompile.destinationDir
//    artifacts.add('archives', task);
//}

